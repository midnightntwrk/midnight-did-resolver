{ pkgs }:

let
  version = builtins.replaceStrings [ "\n" ] [ "" ] (builtins.readFile ../../version);
  callPackageRustCross =
    targetSystem: path: overrides:
    pkgs.pkgsCross."${targetSystem}".callPackage path (
      {
        rust = pkgs.rustTools.mkRustCross {
          pkgsCross = pkgs.pkgsCross."${targetSystem}";
          minimal = true;
        };
      }
      // overrides
    );
  mkNeoprismPackages =
    {
      buildFeatures ? [ ],
      extraPackages ? [ ],
    }:
    rec {
      # neoprism
      neoprism-bin = pkgs.callPackage ./neoprism-bin.nix {
        inherit buildFeatures;
        rust = pkgs.rustTools.rustMinimal;
        inherit (pkgs.rustTools) cargoLock;
      };
      neoprism-bin-x86_64-linux = callPackageRustCross "gnu64" ./neoprism-bin.nix {
        inherit buildFeatures;
        inherit (pkgs.rustTools) cargoLock;
      };
      neoprism-bin-aarch64-linux = callPackageRustCross "aarch64-multiplatform" ./neoprism-bin.nix {
        inherit buildFeatures;
        inherit (pkgs.rustTools) cargoLock;
      };
      neoprism-docker = pkgs.callPackage ./neoprism-docker.nix {
        inherit
          version
          neoprism-bin
          extraPackages
          ;
      };
      neoprism-docker-linux-amd64 = pkgs.pkgsCross.gnu64.callPackage ./neoprism-docker.nix {
        inherit version extraPackages;
        neoprism-bin = neoprism-bin-x86_64-linux;
        tagSuffix = "-amd64";
      };
      neoprism-docker-linux-arm64 =
        pkgs.pkgsCross.aarch64-multiplatform.callPackage ./neoprism-docker.nix
          {
            inherit version extraPackages;
            neoprism-bin = neoprism-bin-aarch64-linux;
            tagSuffix = "-arm64";
          };
    };
  neoprismPackages = mkNeoprismPackages { };
in
{ inherit (pkgs.pkgsInternal) did-midnight-serde; } // neoprismPackages
